//
// FOnline: 2238
// Rotators
//
// quest_caravan_box.fos
//

#include "_macros.fos"
#include "utils_h.fos"
#include "entire_h.fos"
#include "_npc_roles.fos"
#include "economy_h.fos"
#include "entire.fos"

#define BOX_STATUS_NONE       (0)
#define BOX_STATUS_ONGOING    (1)

Critter@ GetCaravanOfficer(Map@ map)
{
    Critter@ npc = map.GetNpc(ROLE_CARAVAN_BOX, FIND_ALL | FIND_ONLY_NPC, 0);
    if(!valid(npc))
    {
        return null;
    }
    return npc;
}

bool d_IsOngoing(Critter& player, Critter@ npc)
{
    return(GetUvar(UVAR_q_box_unload_status, npc.Id, player.Id) == BOX_STATUS_ONGOING);
}

bool d_NotStarted(Critter& player, Critter@ npc)
{
    return(GetUvar(UVAR_q_box_unload_status, npc.Id, player.Id) == BOX_STATUS_NONE);
}

bool d_IsDone(Critter& player, Critter@ npc)
{
    if(GetUvar(UVAR_q_box_unload_status, npc.Id, player.Id) != BOX_STATUS_ONGOING)
        return false;

    return(GetUvar(UVAR_q_box_unload_count, npc.Id, player.Id) == 0);
}

void r_StartQuest(Critter& player, Critter@ npc)
{
    SetUvar(UVAR_q_box_unload_status, npc.Id, player.Id, BOX_STATUS_ONGOING);
    SetUvar(UVAR_q_box_unload_count, npc.Id, player.Id, 10);
}

void r_CompleteQuest(Critter& player, Critter@ npc)
{
    player.AddItem(PID_BOTTLE_CAPS, RewardMod(300));
    player.StatBase[ST_EXPERIENCE] += 500;
    SetUvar(UVAR_q_box_unload_status, npc.Id, player.Id, BOX_STATUS_NONE);
}

void _OnDropBox(Item& item, Critter& cr)
{
    Map@     map = cr.GetMap();

    Critter@ npc = GetCaravanOfficer(map);
    if(!valid(npc))
    {
        DeleteItem(item);
        return;
    }

    array<Entire> entires;
    ParseEntires(map, entires, ENTIRE_CARAVAN_DROP_ZONE);
    for(uint i = 0; i < entires.length(); i++)
    {
        if((entires[i].HexX == item.HexX) && (entires[i].HexY == item.HexY))
        {
            DeleteItem(item);
			GameVar@ var = GetUnicumVar(UVAR_q_box_unload_count, npc.Id, cr.Id);
            if(valid(var))
                var -= 1;
            if(var < 0)
                var = 0;
			else
				cr.StatBase[ST_EXPERIENCE] += 50;
            return;
        }
    }
    if(cr.GetMapId() == npc.GetMapId())
    {
        npc.SayMsg(SAY_SHOUT_ON_HEAD, TEXTMSG_TEXT, 70351);
    }
    DeleteItem(item);
}

void _BoxInit(Item& box, bool firstTime)
{
    box.SetEvent(ITEM_EVENT_DROP, "quest_caravan_box@_OnDropBox");
}

bool _OnUseCaravan(Critter& cr, Scenery& scen, int skill, Item@ item)
{
    if(skill != -1)
        return false;

    if(valid(cr))
    {
        Critter@ npc = cr.GetMap().GetNpc(ROLE_CARAVAN_BOX, FIND_ALL | FIND_ONLY_NPC, 0);

        if(valid(npc) && GetUvar(UVAR_q_box_unload_status, npc.Id, cr.Id) == BOX_STATUS_ONGOING)
        {
            if(GetUvar(UVAR_q_box_unload_count, npc.Id, cr.Id) == 0)
            {
                cr.SayMsg(SAY_NETMSG, TEXTMSG_TEXT, 70352);
                return false;
            }

            if(cr.CountItem(PID_CARAVAN_BOX) > 0)
            {
                cr.SayMsg(SAY_NETMSG, TEXTMSG_TEXT, 70353);
                return false;
            }

            cr.SayMsg(SAY_NETMSG, TEXTMSG_TEXT, 70354);
            Item@ item = cr.AddItem(PID_CARAVAN_BOX, 1);
            if(valid(item))
                item.SetEvent(ITEM_EVENT_DROP, "quest_caravan_box@_OnDropBox");
        }
        else
        {
            if(valid(npc))
                npc.SayMsg(SAY_SHOUT_ON_HEAD, TEXTMSG_TEXT, 70355);
            else
                cr.SayMsg(SAY_NETMSG, TEXTMSG_TEXT, 70355);
            return false;
        }
    }
    return true;
}
